apply plugin: 'com.android.library'

android {
    compileSdkVersion 32
    resourcePrefix 'joda_'

    defaultConfig {
        minSdkVersion 14
        consumerProguardFiles 'proguard-rules.txt'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    namespace 'net.danlew.android.joda'
    testNamespace 'net.danlew.android.joda.test'
    lint {
        lintConfig file('lint.xml')
        textOutput file('stdout')
        textReport true
    }

}

apply plugin: net.danlew.android.joda.TzDataPlugin
apply plugin: "com.vanniktech.maven.publish"

repositories {
    google()
    mavenCentral()
}

dependencies {
    api(libs.jodaTime) {
        artifact {
            classifier = "no-tzdb"
            extension = "jar"
        }
    }

    implementation libs.androidx.startup
    implementation libs.androidx.annotation

    androidTestImplementation libs.junit
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.ext.junit
}

tzdata {
    tzDataDir = new File(project.rootDir, 'tzdata')
}
